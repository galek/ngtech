[GLSL_VERTEX_SHADER]

uniform mat4 u_mvp;
uniform mat4 u_world;
uniform vec3 u_light_dir;
uniform vec3 u_view_pos;

varying vec2 v_tex_coord;
varying vec3 v_light_vec;
varying vec3 v_view_vec;

void main() {
	gl_Position = u_mvp * gl_Vertex; 
	v_tex_coord = gl_MultiTexCoord0.xy;

	vec3 tangent  = normalize((u_world * vec4(gl_MultiTexCoord1.xyz, 0)).xyz); 
	vec3 binormal = normalize((u_world * vec4(gl_MultiTexCoord2.xyz, 0)).xyz); 
	vec3 normal   = normalize((u_world * vec4(gl_Normal.xyz, 0)).xyz); 
	
    vec4 worldPos = u_world * gl_Vertex;

	vec3 lVec = -u_light_dir;
	
	v_light_vec.x = dot(tangent, lVec); 
	v_light_vec.y = dot(binormal, lVec);
	v_light_vec.z = dot(normal, lVec);
	
	vec3 vVec = u_view_pos - worldPos.xyz;
		
	v_view_vec.x = dot(tangent, vVec); 
	v_view_vec.y = dot(binormal, vVec);
	v_view_vec.z = dot(normal , vVec);
}



[GLSL_FRAGMENT_SHADER]

varying vec2 v_tex_coord;
varying vec3 v_light_vec;
varying vec3 v_view_vec;

uniform vec3 u_light_color;
uniform float u_spec_coef;
uniform float u_shininess;

uniform sampler2D u_base_map;
uniform sampler2D u_bump_map;

void main() {

	vec3 lVec = normalize(v_light_vec);
	vec3 vVec = normalize(v_view_vec);
	
	vec4 baseColor = texture2D(u_base_map, v_tex_coord);
	vec3 normal = 2.0 * texture2D(u_bump_map, v_tex_coord).xyz - 1.0;
	
    float diffuse = clamp(dot(normal, lVec), 0.0, 1.0);

	float specular = 0.0;
#ifdef SPECULAR
	specular = pow(clamp(dot(reflect(-vVec, normal), lVec), 0.0, 1.0), u_shininess) * u_spec_coef;
#endif
	
	gl_FragColor = (baseColor * diffuse + specular) * vec4(u_light_color, 1.0);
}
